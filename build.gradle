import com.github.gradle.node.npm.task.NpmTask
import org.jetbrains.kotlin.gradle.dsl.jvm.JvmTargetValidationMode
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.23'
    id 'com.github.node-gradle.node' version "3.4.0"
}

group = 'com.quid'
version = '0.0.1'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

jar {
    enabled = false
}

apply plugin: 'com.github.node-gradle.node'

node {
    nodeProjectDir = file("${project.projectDir}/front")
    version = '21.7.0'
    npmVersion = '10.5.0'
    download = true
}

tasks.register('buildFront', NpmTask) {
    doFirst {
        delete fileTree("${project.projectDir}/src/main/resources/static")
    }
    dependsOn 'npmInstall'
    args = ['run', 'build']
}

tasks.withType(KotlinJvmCompile.class).configureEach {
    jvmTargetValidationMode = JvmTargetValidationMode.WARNING
}

tasks.named('test') {
    useJUnitPlatform()
}

processResources.dependsOn 'buildFront'
